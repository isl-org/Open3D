@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
include(FindPackageHandleStandardArgs)

foreach(dep IN ITEMS @Open3D_3RDPARTY_EXTERNAL_MODULES@)
    find_dependency(${dep})
endforeach()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

foreach(dep IN ITEMS @Open3D_3RDPARTY_HEADER_TARGETS@)
    if(TARGET ${dep})
        get_property(inc TARGET ${dep} PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
        if(inc)
            set_property(TARGET @PROJECT_NAME@::@PROJECT_NAME@ APPEND PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${inc})
        endif()
        unset(inc)
        get_property(def TARGET ${dep} PROPERTY INTERFACE_COMPILE_DEFINITIONS)
        if(def)
            set_property(TARGET @PROJECT_NAME@::@PROJECT_NAME@ APPEND PROPERTY INTERFACE_COMPILE_DEFINITIONS ${def})
        endif()
        unset(def)
    endif()
endforeach()

set_and_check(@PROJECT_NAME@_INCLUDE_DIRS "@PACKAGE_Open3D_INSTALL_INCLUDE_DIR@")
set(@PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@)
set(@PROJECT_NAME@_VERSION "@PROJECT_VERSION_MAJOR@.@PROJECT_VERSION_MINOR@.@PROJECT_VERSION_PATCH@")
set(@PROJECT_NAME@_CONFIG ${CMAKE_CURRENT_LIST_FILE})

find_package_handle_standard_args(@PROJECT_NAME@ CONFIG_MODE)
